buildscript {
    ext {
        junitPlatformVersion = "1.5.2"
        junitJupiterVersion = "5.5.2"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group = "com.ynojima.kodeinsample"
version = "0.1-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    // HTTP
    implementation "io.javalin:javalin:3.6.0"
    implementation "com.squareup.okhttp3:okhttp:4.2.2"

    // Logging
    implementation "org.slf4j:slf4j-simple:1.7.29"

    // Serialization/Deserialization
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.10"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.10"

    // Database
    implementation "org.mariadb.jdbc:mariadb-java-client:2.5.1"
    implementation "com.github.seratch:kotliquery:1.3.1"
    implementation "com.zaxxer:HikariCP:3.4.1"
    implementation "org.flywaydb:flyway-core:6.0.8"

    // Dependency Injection
    implementation "org.kodein.di:kodein-di-generic-jvm:5.3.0"

    // Language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.0"

    // Test Framework
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-engine:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"

    // Assertion
    testImplementation "com.google.truth:truth:1.0"

    // Mock
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    // Lint
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform {
        includeEngines "junit-jupiter"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task fmt(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

jar {
    manifest {
        attributes "Main-Class": "com.ynojima.kodeinsample.MainKt"
    }
}
